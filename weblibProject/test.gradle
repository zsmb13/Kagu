buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
    }
}

apply plugin: com.moowork.gradle.node.NodePlugin

dependencies {
    testCompile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
}

compileTestKotlin2Js {
    kotlinOptions {
        outputFile = "$buildDir/kotlin2js/test/weblib_test.js"
    }
}

compileTestKotlin2Js.configure {
    kotlinOptions.moduleKind = "umd"
}

task populateNodeModules(type: Copy, dependsOn: compileKotlin2Js) {
    from compileKotlin2Js.destinationDir

    configurations.testCompile.each {
        from zipTree(it.absolutePath).matching { include '*.js' }
    }

    into "${buildDir}/node_modules"
}

node {
    download = true
}

task installMocha(type: NpmTask) {
    args = ['install', 'mocha']
}

task runMocha(type: NodeTask, dependsOn: [compileTestKotlin2Js, populateNodeModules, installMocha]) {
    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        script = file('node_modules/mocha/bin/mocha')
    } else {
        script = file('node_modules/.bin/mocha')
    }
    args = [compileTestKotlin2Js.outputFile]
}

test.dependsOn runMocha
